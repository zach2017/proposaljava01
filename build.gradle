plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ai.zactonics'
version = '0.0.1-SNAPSHOT'
description = 'Generate Proposals'

java {
    toolchain {
        def envJavaVersion = System.getenv('JAVA_VERSION')
        def javaVersion = envJavaVersion ? Integer.parseInt(envJavaVersion) : 21
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

bootJar {
  archiveFileName = 'proposalapp.jar'
}

jar {
  enabled = false // donâ€™t create the -plain.jar
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register("buildFrontend", Exec) {
    workingDir = file("frontend/") // path to your Vite project
	  if (System.getProperty('os.name').toLowerCase().contains('windows')) {
                commandLine "cmd", "/c", "yarn", "build"
            } else {
                commandLine "yarn", "build"
            }
}

processResources {
    dependsOn("buildFrontend")
    from("./frontend/dist") {
        into "static"
    }
}
